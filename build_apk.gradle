logger.info("Configuring ${project} (${POM_PACKAGING}) as Android application project")

def projectNameSuffix = project.name.split("-").last()

// Required for generating signed release builds suitable for
// publishing in the app store
//
def simplifiedKeyStore =
  file("$rootDir/release.jks")
def simplifiedKeyAlias =
  project.findProperty('org.librarysimplified.keyAlias')
def simplifiedKeyPassword =
  project.findProperty('org.librarysimplified.keyPassword')
def simplifiedStorePassword =
  project.findProperty('org.librarysimplified.storePassword')

/**
 * This task is run before a release build is assembled.
 */
task preReleaseCheck(group: 'NYPL') {
  description =
    'Verify signing information is present before generating a release build.'

  doLast {
    if (!simplifiedKeyStore.exists()) {
      throw new GradleException(
        "Keystore file '${simplifiedKeyStore}' must exist to sign release builds")
    }
    if (!simplifiedKeyAlias) {
      throw new GradleException(
        "'org.librarysimplified.keyAlias' must be defined to sign release builds")
    }
    if (!simplifiedKeyPassword) {
      throw new GradleException(
        "'org.librarysimplified.keyPassword' must be defined to sign release builds")
    }
    if (!simplifiedStorePassword) {
      throw new GradleException(
        "'org.librarysimplified.storePassword' must be defined to sign release builds")
    }
  }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
  compileSdkVersion android_compile_sdk_version
  buildToolsVersion android_build_tools_version

  defaultConfig {
    minSdkVersion android_min_sdk_version
    targetSdkVersion android_target_sdk_version
  }
  compileOptions {
    encoding 'UTF-8'
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = "1.8"
  }
  signingConfigs {
    release {
      keyAlias simplifiedKeyAlias
      keyPassword simplifiedKeyPassword
      storeFile file("$rootDir/release.jks")
      storePassword simplifiedStorePassword
    }
  }
  buildTypes {
    debug {
      ndk { abiFilters 'x86', 'arm64-v8a', 'armeabi-v7a' }
      versionNameSuffix '-debug'
    }
    release {
      ndk { abiFilters 'arm64-v8a', 'armeabi-v7a' }
      signingConfig signingConfigs.release
    }
  }
  lintOptions {
    checkReleaseBuilds false
  }
  applicationVariants.all { variant ->
    if (variant.buildType.name == 'release') {
      def preBuildTask =
        tasks.findByName("pre${variant.name.capitalize()}Build")
      if (preBuildTask) {
        preBuildTask.dependsOn preReleaseCheck
      }
    }
  }
}

afterEvaluate {
  assemble.dependsOn(bundle)
}
